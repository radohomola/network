# sh mac address-table e.q.:
# Vlan    Mac Address       Type        Ports
#  152    0000.5e00.0101    DYNAMIC     Te1/0/1
#  152    00d7.8f55.f34b    DYNAMIC     Po1
#  152    00d7.8f81.1b4b    DYNAMIC     Te1/0/1
Value MAC_ADDRESS ([0-9a-fA-F]{4}\.[0-9a-fA-F]{4}\.[0-9a-fA-F]{4})
Value TYPE (\S+)
Value VLAN_ID (\S+)
Value List PORT ([^,\s]+)

Start
  ^Destination\s+Address\s+Address\s+Type\s+VLAN\s+Destination\s+Port -> TYPE1
  ^\s+vlan\s+mac address\s+type\s+learn\s+age\s+ports -> TYPE2
  ^\s+vlan\s+mac address\s+type\s+protocols\s+port -> TYPE3
  ^Vlan\s+Mac Address\s+Type\s+Ports -> TYPE4
  # Capture time-stamp if vty line has command time-stamping turned on
  ^Load\s+for\s+
  ^Time\s+source\s+is

TYPE1
  ^${MAC_ADDRESS}\s+${TYPE}\s+${VLAN_ID}\s+${PORT}$$ -> Record
  ^-+\s+-+
  ^Displaying\s+entries
  ^Destination\s+Address\s+Address\s+Type\s+VLAN\s+Destination\s+Port
  ^\s*$$
  ^Multicast\s+Entries -> End
  ^. -> Error

TYPE2
  # Order of the group in brackets here matters
  ^\s*(\*\s+R|\*|R|\s)\s*(\d+|-+|[Nn]/[Aa]) -> Continue.Record
  # using 20 spaces should ensure that lines only match destination port flowing to next line
  ^\s{20}\s+${PORT}(\s+|,\s*)\S -> Continue
  ^\s{20}\s+${PORT},*\s*$$
  ^\s{20}\s+[^,\s]+(\s+|,\s*)${PORT}(\s+|,\s*)\S -> Continue
  ^\s{20}\s+[^,\s]+(\s+|,\s*)${PORT},*\s*$$
  ^\s{20}\s+([^,\s]+(\s+|,\s*)){2}${PORT}(\s+|,\s*)\S -> Continue
  ^\s{20}\s+([^,\s]+(\s+|,\s*)){2}${PORT},*\s*$$
  ^\s{20}\s+([^,\s]+(\s+|,\s*)){3}${PORT}(\s+|,\s*)\S -> Continue
  ^\s{20}\s+([^,\s]+(\s+|,\s*)){3}${PORT},*\s*$$
  ^\s{20}\s+([^,\s]+(\s+|,\s*)){4}${PORT}(\s+|,\s*)\S -> Continue
  ^\s{20}\s+([^,\s]+(\s+|,\s*)){4}${PORT},*\s*$$
  ^\s{20}\s+([^,\s]+(\s+|,\s*)){5}${PORT}(\s+|,\s*)\S -> Continue
  ^\s{20}\s+([^,\s]+(\s+|,\s*)){5}${PORT},*\s*$$
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s*$$
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+${PORT}(\s+|,\s*)\S -> Continue
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+${PORT},*\s*$$
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+[^,\s]+(\s+|,\s*)${PORT}(\s+|,\s*)\S -> Continue
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+[^,\s]+?(\s+|,\s*)${PORT},*\s*$$
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+([^,\s]+(\s+|,\s*)){2}${PORT}(\s+|,\s*)\S -> Continue
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+([^,\s]+(\s+|,\s*)){2}${PORT},*\s*$$
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+([^,\s]+(\s+|,\s*)){3}${PORT}(\s+|,\s*)\S -> Continue
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+([^,\s]+(\s+|,\s*)){3}${PORT},*\s*$$
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+([^,\s]+(\s+|,\s*)){4}${PORT}(\s+|,\s*)\S -> Continue
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+([^,\s]+(\s+|,\s*)){4}${PORT},*\s*$$
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+([^,\s]+(\s+|,\s*)){5}${PORT}(\s+|,\s*)\S -> Continue
  ^\s*(\*\s+R|\*|R|\s)\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+\S+\s+([^,\s]+(\s+|,\s*)){5}${PORT},*\s*$$
  ^-+\+-+
  ^Displaying\s+entries
  ^\s+vlan\s+mac address\s+type\s+learn\s+age\s+ports
  ^\s*$$
  ^Multicast\s+Entries -> End
  ^. -> Error

TYPE3
  ^\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+\S+\s+${PORT}(?:\s|$$) -> Record
  ^-+\+-+
  ^Displaying\s+entries
  ^\s+vlan\s+mac address\s+type\s+protocols\s+port
  ^\s*$$
  ^Multicast\s+Entries -> End
  ^. -> Error

TYPE4
  ^\s*${VLAN_ID}\s+${MAC_ADDRESS}\s+${TYPE}\s+${PORT}(?:\s|$$) -> Record
  ^-+\s+-+
  ^Displaying\s+entries
  ^Vlan\s+Mac Address\s+Type\s+Ports
  ^\s*$$
  ^Total\s+Mac\s+Addresses
  ^MultiCast\s+Entries -> End
  ^. -> Error
